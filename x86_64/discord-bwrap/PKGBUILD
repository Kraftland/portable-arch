# Maintainer: Kimiblock Moe
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Morgan <morganamilo@archlinux.org>
# Contributor: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Robin Candau <antiz@archlinux.org>

pkgname=discord-portable
_pkgname=Discord
pkgver=0.0.121
pkgrel=1
epoch=1
pkgdesc="All-in-one voice and text chat for gamers. Sandboxed for privacy."
arch=('x86_64')
url='https://discord.com'
license=('custom')
conflicts=("discord")
makedepends+=("discord=1:$(echo ${pkgver} | sed 's|.portable||g')" portable)
provides+=("discord" "discord-sandbox" "discord-portable")
options=(!debug !strip)
replaces+=(discord-bwrap)
source=(
	"portable-config"
	"discord.sh"
	"discord.desktop"
	)
sha512sums=('638bce366dba72aee38b2fe00675f0773596825b7d1a7e2f2b76519a2e3626e27e40dd0f91c5b08466cc22938885d3dd6d77a2b43ba7669d6d13ebd85ad37d00'
            'a881db4d5f76c8f2f869b0565e6e757b0c462341d2911b24aa8da67e21dbe739fc38ede18486d42a179644a8389355654408d7947ffd6ef0052a4360fb15d91f'
            '2bbac6cd293e231ae93b0f58eecd6a6217b8299d2dbbe2d06a329297fa353506f6e90070aef363445ebd863c0d302bebb7d4af48a4858c25c1ef9b3924feb53b')

function prepare() {
	echo '''{
  "SKIP_HOST_UPDATE": true
}''' >"${srcdir}/skip-update.json"
}

function pkgver() {
	pacman -Q discord | awk '{sub(/-.*/, "", $2); print $2}' | sed "s|${epoch}:||g"
}

package() {
	depends=('libnotify' 'libxss' 'nspr' 'nss' 'gtk3' libpulse libappindicator-gtk3 "portable")
	export pkgdir
	export pkgname
	export srcdir
	portable-packer \
		-v \
		--distro arch \
		--mode copy discord \
		--hash true \
		--config "${srcdir}/portable-config" \
		--desktop-file "${srcdir}/discord.desktop"

	install -Dm755 discord.sh "${pkgdir}/usr/bin/Discord"
	install -Dm644 "${srcdir}/skip-update.json" "${pkgdir}/usr/share/discord-bwrap/settings.json"
}
