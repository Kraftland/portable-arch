# Maintainer: Kimiblock Moe
# Contributor: Purofle <purofle@gmail.com>
# Contributor: Integral <integral@member.fsf.org>

pkgname=linuxqq-portable
pkgver=3.2.25_45758
pkgrel=1
epoch=1
pkgdesc="Linux QQ based on Electron, sandboxed by portable"
arch=('x86_64' 'aarch64' 'loong64')
url="https://im.qq.com/linuxqq"
license=('LicenseRef-QQ')
conflicts=("linuxqq")
provides=("linuxqq")
depends=('nss' 'alsa-lib' 'gtk3' 'at-spi2-core' 'libvips' 'openjpeg2' 'openslide' 'portable')
optdepends=('libappindicator-gtk3: Allow QQ to extend a menu via Ayatana indicators in Unity, KDE or Systray (GTK+ 3 library).')
_md5_prefix=7516007c
_src_prefix="linuxqq_${pkgver/_/-}"
source_x86_64=("https://dldir1.qq.com/qqfile/qq/QQNT/${_md5_prefix}/${_src_prefix}_amd64.deb")
source_aarch64=("https://dldir1.qq.com/qqfile/qq/QQNT/${_md5_prefix}/${_src_prefix}_arm64.deb")
source_loong64=("https://dldir1.qq.com/qqfile/qq/QQNT/${_md5_prefix}/${_src_prefix}_loongarch64.deb")
source=("linuxqq.sh" "start.sh" "portable-config" "qq.desktop")
sha512sums=('5132db083f632c6f4ad7c77f9d3994392c30fac01697f0fec4010cb5daccb33e7d3d56e7a13daaa405a175d0f0e896ae7addc56c9946539389cb4f80bea6ab27'
            'b6a955e64b69d98776346a5ec154d119e1acdc3bba364bf01bdcd6119ce20933f326d40990e7e477c3b7c741beb50e22da4ad2d6880538256245bdc31dcede0e'
            'cf16b3e443633299e5f9632bce91df1dbceb7a0d041c722c38ff8044cc1da6aaa85ed23da72c24b74eb0a203b62aa65a6ce01fcb8ae71c6fa858b7bbb32d0646'
            'ba310fcec0a795391f60348d7bf8379ce3110dcca6d1be7435b9b50cb8271dcedfb16d7607661cf39093f294dde45eab85c84a7436eea9c4adf0743183da4253')
sha512sums_x86_64=('9cef60b2931914fea99ea2c39203999f25a8d525625d36c867c9a9d80dbfb5729a2509387e10e92d22f94dc0bae1d8b744763f452e2130cdd7b343b1fe5c2dd3')
sha512sums_aarch64=('bfc6a900d4c955a574249411e4719296194bd102205064295e07b2b357d2b6496384d074164aaafd187c0b9b75d80c5ae190c494e14c7a8df717d88ddaf70ca2')
sha512sums_loong64=('af6a32c4579c8f69e86c34ee441360e95f658ec10e94b95354d21e7e0a6adb6c535e172c795236541b9e44fa9dd2d6b20b6d68a3fc5d5bc76681c2d99e0cb7ce')
options=('!strip' '!debug')

package() {
  echo "  -> Extracting the data.tar.xz..."
  bsdtar -xf data.tar.xz -C "${pkgdir}/"

  # License
  install -Dm644 "${pkgdir}/opt/QQ/LICENSE.electron.txt" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 "${pkgdir}/opt/QQ/LICENSES.chromium.html" -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Temporary Solution: Remove libvips and libssh2 which comes from package "linuxqq" itself
  rm -fv ${pkgdir}/opt/QQ/resources/app/{sharp-lib/libvips-cpp.so.42,{,avsdk/bugly/}libssh2.so.1}
  install -Dm755 "${srcdir}/portable-config" "${pkgdir}/usr/lib/portable/info/im.qq.app/config"
  install -Dm755 "${srcdir}/linuxqq.sh" "${pkgdir}/usr/lib/portable/info/im.qq.app/start-script.sh"
  install -Dm755 "${srcdir}/start.sh" "${pkgdir}/usr/bin/linuxqq"
  install -Dm644 "${srcdir}/qq.desktop" "${pkgdir}/usr/share/applications/im.qq.app.desktop"
  rm "${pkgdir}/usr/share/applications/qq.desktop"
}
